Import("env")

env.Append(
    CPPPATH=[
        # "#/lib/mbedtls",
        "#/lib/mbedtls/include",
    ],
    SDK_HEADERS=[
        File("mbedtls/include/mbedtls/des.h"),
        File("mbedtls/include/mbedtls/sha1.h"),
        File("mbedtls/include/mbedtls/sha256.h"),
        File("mbedtls/include/mbedtls/md5.h"),
        # File("mbedtls/include/mbedtls/sha1.h"),
    ],
    CPPDEFINES=[("MBEDTLS_CONFIG_FILE", '\\"mbedtls_cfg.h\\"')],
)


libenv = env.Clone(FW_LIB_NAME="mbedtls")
libenv.ApplyLibFlags()

libenv.AppendUnique(
    CCFLAGS=[
        # Required for lib to be linkable with .faps
        "-mword-relocations",
        "-mlong-calls",
        # Crappy code :)
        "-Wno-redundant-decls",
    ],
)

sources = libenv.GlobRecursive("*.c*", "mbedtls/library")

lib = libenv.StaticLibrary("${FW_LIB_NAME}", sources)
Depends(sources, File("mbedtls_cfg.h"))
libenv.Install("${LIB_DIST_DIR}", lib)
Return("lib")
